plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = 'tn.eluea.kgpt'
    compileSdk = 36

    defaultConfig {
        applicationId = "tn.eluea.kgpt"
        minSdk = 24
        targetSdk = 36
        versionCode = 10
        versionName = "4.0.10"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile = file("${System.getProperty('user.home')}/.android/debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
            signingConfig = signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE',
                         'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE',
                         'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0',
                         'META-INF/*.kotlin_module']
        }
    }

    lint {
        // This project currently has many legacy lint issues; don't fail the build on lint.
        abortOnError = false
        checkReleaseBuilds = false
    }

    buildFeatures {
        buildConfig = true
    }
}

// Ensure the debug keystore exists when signing release builds with the debug key.
// This avoids CI failures like: "debug.keystore not found".
afterEvaluate {
    def debugKeystore = file("${System.getProperty('user.home')}/.android/debug.keystore")
    def osName = System.getProperty('os.name').toLowerCase()
    def keytoolPath = new File(System.getProperty('java.home'),
            'bin/keytool' + (osName.contains('win') ? '.exe' : '')).absolutePath

    def ensureDebugKeystore = tasks.register('ensureDebugKeystore') {
        doLast {
            if (!debugKeystore.exists()) {
                debugKeystore.parentFile.mkdirs()
                exec {
                    commandLine keytoolPath,
                            '-genkeypair', '-v',
                            '-keystore', debugKeystore.absolutePath,
                            '-storepass', 'android',
                            '-alias', 'androiddebugkey',
                            '-keypass', 'android',
                            '-dname', 'CN=Android Debug,O=Android,C=US',
                            '-keyalg', 'RSA',
                            '-keysize', '2048',
                            '-validity', '10000'
                }
            }
        }
    }

    // Hook into both debug/release flows; harmless if tasks don't exist.
    tasks.matching { it.name == 'validateSigningRelease' || it.name == 'validateSigningDebug' }.configureEach {
        dependsOn ensureDebugKeystore
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        def appName = "KGPT"
        def buildType = variant.buildType.name
        def versionName = variant.versionName
        def versionCode = variant.versionCode
        outputFileName = "${appName}-${buildType}-v${versionName}.apk"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'com.google.guava:guava:31.0.1-android'
    implementation 'org.reactivestreams:reactive-streams:1.0.4'

    // WorkManager for background update checking
    implementation 'androidx.work:work-runtime:2.9.0'
    
    // DocumentFile for folder picker
    implementation 'androidx.documentfile:documentfile:1.0.1'

    compileOnly 'de.robv.android.xposed:api:82'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}